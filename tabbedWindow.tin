#VARIABLE {comm_window}
{
    {active} {comm}
    {topcol} {1}
    {toprow} {1}
    {botcol} {$oneThirdColsVar}
    {botrow} {10}
    {offset} {0}
    {tab} 
    {
        {comm}      {}
        {clan}      {}
        {ooc}       {}
	{broadcasts}{}
    }
    {unread} 
    {
        {comm}      {0}
        {clan}      {0}
        {ooc}       {0}
	{broadcasts}{0}
    }
}

#ALIAS {comm_window_scroll_up}
{
    #if {$comm_window[offset] < &comm_window[tab][%0][]}
    {
        #math comm_window[offset] $comm_window[offset] + 1;
        comm_window_draw_data $comm_window[active]
    }
}

#ALIAS {comm_window_scroll_down}
{
    #if {$comm_window[offset] > 1}
    {
        #math comm_window[offset] $comm_window[offset] - 1;
        comm_window_draw_data $comm_window[active]
    }
}

#BUTTON {$comm_window[toprow] $comm_window[topcol] $comm_window[botrow] $comm_window[botcol] SCROLLED MOUSE WHEEL UP}
{
    comm_window_scroll_up $comm_window[active]
}

#BUTTON {$comm_window[toprow] $comm_window[topcol] $comm_window[botrow] $comm_window[botcol] SCROLLED MOUSE WHEEL DOWN}
{
    comm_window_scroll_down
}

#FUNCTION {comm_window_square}
{
    #return $comm_window[toprow]+1 $comm_window[topcol]+1 $comm_window[botrow]-1 $comm_window[botcol]-1
}

#FUNCTION {comm_window_border}
{
    #return $comm_window[toprow] $comm_window[topcol] $comm_window[botrow] $comm_window[botcol]
}

#ALIAS {comm_window_draw_data}
{
    #draw tile @comm_window_square{} $comm_window[tab][%1][-$comm_window[offset]-10..-$comm_window[offset]]
}

#ALIAS {comm_window_show %1 %2}
{
    #list comm_window[tab][%1] ins -1 {%2};
    #if {{%1} === {$comm_window[active]}}
    {
        comm_window_draw_data %1
    };
    #elseif {$comm_window[unread][%1] == 0}
    {
        #variable comm_window[unread][%1] 1;
        comm_window_draw_tabs
    }
}

#ALIAS {comm_window_draw_tabs}
{
    #draw $colorTwo $colorThree $boxvar @comm_window_border{};
    #local short {};
    #local index {};
    #loop {1} {&comm_window[tab][]} {index}
    {
        #local tab *comm_window[tab][+$index];
        #if {{$comm_window[active]} === {$tab}}
        {
            #nop read tabs;
            #format short <138>%.6s $tab
        };
        #elseif {$comm_window[unread][$tab] == 1}
        {
            #nop unread tab;
            #format short <118>%.6s $tab
        };
        #else
        {
            #nop selected tab;
            #format short <178>%.6s $tab
        };
        #line ignore #showme {\e]68;2;TAB;comm_window_tab_click $tab\a\e[4;24m$short\e[24m} {$comm_window[toprow]} {$comm_window[topcol] - 5 + $index * 7}
    };
}

#ALIAS {comm_window_tab_click}
{
    #variable comm_window[active] %0;
    #variable comm_window[unread][%0] 0;
    #variable comm_window[offset] 1;
    comm_window_draw_tabs;
    comm_window_draw_data %0
}

#EVENT {PRESSED SECURE LINK TAB MOUSE BUTTON ONE}
{
    %4
}

#ALIAS {test}
{
    #config mouse on;
    comm_window_draw_tabs;
}
